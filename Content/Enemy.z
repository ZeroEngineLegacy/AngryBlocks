class Enemy : ScriptComponent
{
    [Property]
    var NotFalling : SpriteSource = SpriteSource.Find("Enemy");
    
    [Property]
    var Falling : SpriteSource = SpriteSource.Find("EnemyMoving");
    
    [Property]
    var VelocityThreshold : Real = 0.5;
    
    [Dependency]
    var RigidBody : RigidBody = null;
    
    [Dependency]
    var Sprite : Sprite = null;
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, "Death", this.OnDeath);
        
        ++this.LevelSettings.GameLogic.EnemiesLeft;
    }
    
    function OnDeath(e : Event)
    {
        var gameLogic = this.LevelSettings.GameLogic;
        --gameLogic.EnemiesLeft;
        
        if (gameLogic.EnemiesLeft == 0)
        {
            gameLogic.State = GameState.Won;
            gameLogic.ShowText("You win!");
        }
    }

    function OnLogicUpdate(updateEvent : UpdateEvent)
    {
        if (Math.Length(this.RigidBody.Velocity) > this.VelocityThreshold)
        {
            this.Sprite.SpriteSource = this.Falling;
        }
        else
        {
            this.Sprite.SpriteSource = this.NotFalling;
        }
    }
}
